plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id "com.diffplug.spotless" version "6.23.3"
	id 'org.openapi.generator' version '7.2.0'
}

group = 'ru.mentee.library'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
		versions = [
			// Плагины
			spotless          : '6.23.3',

			// Инструменты
			googleJavaFormat : '1.17.0'
	]
	set('testcontainersVersion', "1.19.7")
	set('springCloudVersion', "2023.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	implementation 'org.liquibase:liquibase-core'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
	implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'  // Для блога (in-memory БД)
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// MapStruct для маппинга Entity <-> DTO
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// JPA Metamodel Generator для типобезопасных Specifications
	annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen:6.4.4.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'io.rest-assured:json-path'
	testImplementation 'io.rest-assured:xml-path'
	testImplementation 'com.h2database:h2'
	testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
	testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// OpenAPI Generator configuration
openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/api/task-management-api.yaml"
	outputDir = "$buildDir/generated"
	apiPackage = "ru.mentee.taskmanager.api.generated.controller"
	modelPackage = "ru.mentee.taskmanager.api.generated.dto"
	configOptions = [
		dateLibrary: "java8",
		interfaceOnly: "true",
		useTags: "true",
		performBeanValidation: "true",
		useBeanValidation: "true",
		useSpringBoot3: "true",
		delegatePattern: "true"
	]
}

// Генерация для Order Management API
// Плагин поддерживает только один блок openApiGenerate
// Для генерации Order API временно измените в блоке выше (строки 63-66):
//   inputSpec = "$rootDir/src/main/resources/api/order-management-api.yaml"
//   apiPackage = "ru.mentee.ordermanager.api.generated.controller"
//   modelPackage = "ru.mentee.ordermanager.api.generated.dto"
// Затем запустите: ./gradlew openApiGenerate
// После генерации верните все значения обратно для Task Management API

// Подключение сгенерированного кода к проекту
sourceSets {
	main {
		java {
			srcDirs += "$buildDir/generated/src/main/java"
		}
	}
}

// Зависимость от генерации перед компиляцией
compileJava.dependsOn tasks.openApiGenerate

// Указываем главный класс для Spring Boot (чтобы избежать конфликта с TodoApplication)
tasks.named('bootJar') {
	mainClass = 'ru.mentee.library.LibraryApplication'
}

tasks.named('bootRun') {
	mainClass = 'ru.mentee.library.LibraryApplication'
}

// Для resolveMainClassName
tasks.named('resolveMainClassName') {
	mainClassName = 'ru.mentee.library.LibraryApplication'
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		// IMPORTANT:
		// - `openApiGenerate` writes sources into `build/generated/...`
		// - Spotless must NOT format those generated sources, otherwise Gradle task validation
		//   can fail in CI with "implicit dependency" errors.
		target 'src/**/*.java'
		targetExclude "${buildDir}/generated/**"
		googleJavaFormat("${versions.googleJavaFormat}").aosp().reflowLongStrings().skipJavadocFormatting()
		formatAnnotations()
		licenseHeader '/* @MENTEE_POWER (C)$YEAR */'
	}
}

// Ensure generation happens before compilation (already set above) and keep Spotless independent
// from generated sources by excluding build/generated/** in the Spotless config.
